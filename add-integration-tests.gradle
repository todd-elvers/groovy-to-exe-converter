/* The following assumes the 'idea' gradle plugin as already been applied at this point */

// Add integration directory to source sets
sourceSets {
    integrationTest { sourceSet ->
        ["java", "groovy", "scala", "resources"].each {
            if (!sourceSet.hasProperty(it)) return
            sourceSet."$it".srcDir file("src/integration/${it}")
        }
    }
}

// Ensure IDEA highlights the integration folders appropriately
idea {
    module {
        testSourceDirs += file ('src/integration/groovy')
        testSourceDirs += file ('src/integration/resources')
    }
}

// Setup dependencies for integration testing
dependencies {
    integrationTestCompile sourceSets.main.output
    integrationTestCompile sourceSets.test.output
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime

    // When running integration tests, use the resources from src/main/resources
    integrationTestCompile files(sourceSets.main.output.resourcesDir)
}

// Task to run all integration tests
task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    reports.junitXml.destination = file("${project.testResultsDir}/$name")
    reports.html.destination = file("${project.reporting.baseDir}/$name")

    shouldRunAfter test
}

// Make sure 'check' task calls integration test
check.dependsOn integrationTest